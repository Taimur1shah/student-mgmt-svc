plugins {
    id 'java'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id "org.openapi.generator" version "6.2.1"
}

group 'com.skiply.student'
version '1.0-SNAPSHOT'
sourceCompatibility = '17'
targetCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}


dependencies {
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation 'org.springframework.hateoas:spring-hateoas:2.0.1'
    implementation 'io.springfox:springfox-swagger2:3.0.0'
    implementation "org.openapitools:jackson-databind-nullable:0.2.4"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.14.0"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.14.1"
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'
    implementation 'org.springframework.boot:spring-boot-starter-actuator:3.5.0'
    implementation 'jakarta.validation:jakarta.validation-api:3.0.2'
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'

    implementation project(":student-application")
    implementation project(":student-domain")
}


bootJar {
    enabled = false
}

jar {
    enabled = true
}

openApiGenerate {
    println "OpenApi Generator Started . . . . . . ."
    generatorName = "spring"
    inputSpec = file("src/main/resources/student-openapi-spec-file.yaml").absolutePath.replace('\\', '/')
    outputDir = "$buildDir/generated/openapi".toString()
    apiPackage = "com.skiply.student.api"
    modelPackage = "com.skiply.student.api.model"
    invokerPackage = "com.skiply.student.api.invoker"
    globalProperties = [
            modelDocs   : "true",
            apiDocs     : "true",
            modelTests  : "false",
            apiTests    : "false"
    ]
    configOptions = [
            useTags                : "true",
            delegatePattern        : "false",
            hateoas                : "true",
            requestMappingMode     : "controller",
            interfaceOnly          : "true",
            useSpringBoot3  : "true",
            dateLibrary     : "java8",
            openApiSpecFileLocation: "$projectDir/src/main/resources"
    ]
}

sourceSets {
    getByName("main") {
        java {
            srcDir("$buildDir/generated/openapi/src/main/java")
        }
    }
}


compileJava.dependsOn(tasks.openApiGenerate)

tasks.named('test') {
    useJUnitPlatform()
}